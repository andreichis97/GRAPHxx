CC "Modeling" GET_ACT_MODEL
SETL actModelId: (modelid)

CC "Core" GET_CLASS_ID classname:"Data"
SETL dataClassId: (classid)

CC "Core" GET_CLASS_ID classname:"HasAttribute"
SETL hasAttributeClassId: (classid)

CC "Modeling" GET_SELECTED  modelid:(actModelId)

SETL selectedObj: (VAL token(objids, 0, " "))

CC "Core" GET_CLASS_ID objid:(selectedObj)
CC "Core" GET_CLASS_NAME classid:(classid)

IF (classname = "ThingIdentifier")
{
    CC "Core" GET_ATTR_VAL objid:(selectedObj) attrname:("Label")
    SETL currentLabel:(val)
    CC "Core" GET_ATTR_VAL objid:(selectedObj) attrname:("Position")
    SETL currentPosition:(val)

    CC "Core" CREATE_OBJ modelid:(actModelId) classid:(dataClassId)
    SETL createdObjId:(objid)
    CC "Core" SET_ATTR_VAL objid:(createdObjId) attrname:("Value") val:(currentLabel)
    CC "Core" SET_ATTR_VAL objid:(createdObjId) attrname:("Position") val:(currentPosition)

    CC "Core" GET_CONNECTORS objid:(selectedObj) in

    FOR i in:(objids)
    {
        CC "Core" GET_CLASS_ID objid:(VAL i)
        CC "Core" GET_CLASS_NAME classid:(classid)
        IF(classname = "RelatedTo")
        {
            SETL connectorID:(VAL i)

            CC "Core" GET_ATTR_VAL objid:(connectorID) attrname:("Prefix")
            SETL connectorPrefix:(val)
            CC "Core" GET_ATTR_VAL objid:(connectorID) attrname:("Local identifier")
            SETL connectorLocalIdentifier:(val)

            CC "Core" GET_CONNECTOR_ENDPOINTS objid:(connectorID)
            SETL fromObj:(fromobjid)

            CC "Core" CREATE_CONNECTOR modelid:(actModelId) fromobjid:(fromObj) toobjid:(createdObjId) classid:(hasAttributeClassId)
            CC "Core" SET_ATTR_VAL objid:(objid) attrname:("Prefix") val:(connectorPrefix)
            CC "Core" SET_ATTR_VAL objid:(objid) attrname:("Local identifier") val:(connectorLocalIdentifier)
        }
    }
    CC "Core" DELETE_OBJ modelid:(actModelId) objid:(selectedObj)
}
ELSE
{
    CC "AdoScript" VIEWBOX text:("This operation is permitted only for ThingIdentifier elements!")
}