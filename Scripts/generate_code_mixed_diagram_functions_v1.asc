SETL t1: (getTickCount())

PROCEDURE global GET_MODEL_PREFIXES modelId: integer result: reference
{
    SETL prefixesValues:("")
    CC "Core" GET_CLASS_ID objid:(modelId)
    CC "Core" GET_CLASS_NAME classid:(classid)
    CC "Core" GET_ATTR_ID classid:(classid) attrname:"ModelPrefixes"
    SETL modelPrefixesId:(attrid)
    #attrid
    CC "Core" GET_ALL_REC_ATTR_ROW_IDS objid:(modelId) attrid:(modelPrefixesId)
	FOR i in:(rowids)
	{
		CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Prefix"
		SETL prefixValue:(val)
		CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Dictionary"
		SETL dictionaryValue:(val)
        SETL prefixesValues:(prefixesValues + "@prefix " + prefixValue + " " + dictionaryValue + ".\n")
	}
    SETL prefixesValues:(prefixesValues + "\n")
    SETL result:(prefixesValues)
}

PROCEDURE global GET_THINGTYPE_INFO objectId: integer uriValue: string message: string result: reference   
{
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"URI"
    SETL objType:(val)
    SETL result:(message + uriValue + " a " + objType + ".\n")
}

PROCEDURE global GET_THINGTYPE_INFO_FULL connectorId: integer objectId: integer uriValue: string message: string result: reference   
{
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"URI"
    SETL objType:(val)
    CC "Core" GET_CLASS_ID objid:(connectorId)
    SETL connectorObjId:(connectorId)
    CC "Core" GET_CLASS_NAME classid:(classid)
    CC "Core" GET_ATTR_VAL objid:(connectorId) attrname:"URI"
    IF(classname = "IsOfType")
    {
        SETL relType:("a")
    }
    IF(classname = "To")
    {
        SETL relType:("rdfs:range")
    }
	IF(classname = "From")
    {
        SETL relType:("rdfs:domain")
    }
    IF (classname = "RelatedTo")
    {
        CC "Core" GET_ATTR_VAL objid:(connectorId) attrname:"URI"
        SETL relType:(val)
    }
    IF (classname = "IsAKindOf")
    {
       SETL relType:("rdfs:subClassOf")
    }
    SETL result:(message + uriValue + " " + relType + " " + objType + ".\n")
}

PROCEDURE global GET_THINGIDENTIFIER_INFO connectorId: integer objectId: integer uriValue: string message: string result: reference   
{
    CC "Core" GET_CLASS_ID objid:(connectorId)
    CC "Core" GET_CLASS_NAME classid:(classid)
    IF(classname != "IsOfType")
    {
        CC "Core" GET_ATTR_VAL objid:(connectorId) attrname:"URI"
        SETL relType:(val)
        CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"URI"
        SETL objType:(val)
        SETL result:(message + uriValue + " " + relType + " " + objType + ".\n")
    }
    ELSE 
    {
        CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"URI"
        SETL objType:(val)
        SETL result:(message + uriValue + " " + "a" + " " + objType + ".\n")
    }
}

PROCEDURE global GET_PROPERTY_INFO connectorId: integer objectId: integer uriValue: string message: string result: reference   
{
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"URI"
    SETL objType:(val)
    CC "Core" GET_CLASS_ID objid:(connectorId)
    SETL connectorObjId:(connectorId)
    CC "Core" GET_CLASS_NAME classid:(classid)
    IF(classname = "IsOfType")
    {
        SETL relType:("a")
        SETL result:(message + uriValue + " " + relType + " " + objType + ".\n")
    }
    IF (classname = "From") 
    {
        SETL relType:("rdfs:domain")
        SETL result:(message + objType + " " + relType + " " + uriValue + ".\n")
    }
    IF (classname = "To") 
    {
        SETL relType:("rdfs:range")
        SETL result:(message + uriValue + " " + relType + " " + objType + ".\n")
    }
    IF (classname = "RelatedTo")
    {
        CC "Core" GET_ATTR_VAL objid:(connectorId) attrname:"URI"
        SETL relType:(val)
        SETL result:(message + uriValue + " " + relType + " " + objType + ".\n")
    }
    IF (classname = "IsAKindOf")
    {
        #CC "Core" GET_ATTR_VAL objid:(connectorId) attrname:"URI"
        SETL relType:("rdfs:subPropertyOf")
        #SETL result:(message + uriValue + " " + relType + " " + objType + ".\n")
    }
    SETL result:(message + uriValue + " " + relType + " " + objType + ".\n")
}

PROCEDURE global GET_DATATYPE_NODE_INFO connectorId: integer objectId: integer uriValue: string message: string result: reference   
{
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"Datatype"
    SETL dataType:(val)
    SETL connectorObjId:(connectorId)
    CC "Core" GET_CLASS_ID objid:(connectorId)
    SETL connectorObjId:(connectorId)
    CC "Core" GET_CLASS_NAME classid:(classid)
    IF(classname = "To")
    {
        SETL relType:("rdfs:range")
    }
    ELSE
    {
        CC "Core" GET_ATTR_VAL objid:(connectorId) attrname:"URI"
        SETL relType:(val)
    }
    SETL result:(message + uriValue + " " + relType + " " + dataType + ".\n")
}

PROCEDURE global GET_DATA_INFO connectorId: integer objectId: integer uriValue: string message: string result: reference   
{
    CC "Core" GET_ATTR_VAL objid:(connectorId) attrname:"URI"
    SETL relType:(val)
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"Value"
    SETL dataValue:(val)
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"Datatype"
    SETL dataType:(val)
    SETL result:(message + uriValue + " " + relType + " " + "\"" + dataValue + "\"" + "^^" + dataType + ".\n")
}

PROCEDURE global GET_HELPER_NODE_INFO connectorId: integer objectId: integer uriValue: string message: string result: reference   
{
    CC "Core" GET_ATTR_VAL objid:(connectorId) attrname:"URI"
    SETL relType:(val)
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"Local identifier"
    SETL localIdentifierValue:(val)
    SETL helperNodeUriValue:("_:" + localIdentifierValue)
    CC "Core" GET_CLASS_ID objid:(objectId)
    CC "Core" GET_ATTR_ID classid:(classid) attrname:"IsOfType"
    SETL attributesId:(attrid)
    CC "Core" GET_INTERREF objid:(objectId) attrid:(attributesId)
    CC "Core" GET_INTERREF_COUNT objid:(objectId) attrid:(attributesId)
    SETL intrfCount:(count)
    IF(intrfCount > 0)
    {
        SETL modelIdToEnter:(tmodelid)
        SETL thingTypeObjName:(tobjname)
        SETL thingTypeObjClassId:(tclassid)
        CC "Modeling" IS_OPENED modelid:(modelIdToEnter)
        IF (NOT(isopened))
        {
            CC "Modeling" OPEN modelids:(modelIdToEnter)
        }
        CC "Core" GET_OBJ_ID modelid:(modelIdToEnter) classid:(thingTypeObjClassId) objname:(thingTypeObjName)
        CC "Core" GET_ATTR_VAL objid:(objid) attrname:"URI"
        SETL helperNodeType:(val)
        SETL result:(message + uriValue + " " + relType + " " + helperNodeUriValue + ".\n" + helperNodeUriValue + " a " + helperNodeType + ".\n")
    }
    ELSE
    {
        SETL result:(message + uriValue + " " + relType + " " + helperNodeUriValue + ".\n")
    }
}

PROCEDURE global GET_LIST_CONTAINER_INFO fromObject: integer connectorId: integer objectId: integer uriValue: string message: string result: reference   
{
    CC "Core" GET_ATTR_VAL objid:(fromObject) attrname:"Label"
    SETL labelVal:(val)
    CC "Core" GET_ATTR_VAL objid:(connectorId) attrname:"URI"
    SETL relType:(val)
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"Name"
    SETL nameValue:(val)
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"Prefix"
    SETL prefixValue:(val)
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"Local identifier"
    SETL localIdentifierValue:(val)
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"ContainerType"
    SETL containerType:(val)
    CC "Core" GET_CLASS_ID objid:(objectId)
    CC "Core" GET_ATTR_ID classid:(classid) attrname:"IsOfType"
    SETL attributesId:(attrid)
    CC "Core" GET_INTERREF objid:(objectId) attrid:(attributesId)
    CC "Core" GET_INTERREF_COUNT objid:(objectId) attrid:(attributesId)
    SETL intrfCount:(count)
    IF(intrfCount > 0 AND relType != labelVal)
    {
        SETL modelIdToEnter:(tmodelid)
        SETL thingTypeObjName:(tobjname)
        SETL thingTypeObjClassId:(tclassid)
        CC "Modeling" IS_OPENED modelid:(modelIdToEnter)
        IF (NOT(isopened))
        {
            CC "Modeling" OPEN modelids:(modelIdToEnter)
        }
        CC "Core" GET_OBJ_ID modelid:(modelIdToEnter) classid:(thingTypeObjClassId) objname:(thingTypeObjName)
        CC "Core" GET_ATTR_VAL objid:(objid) attrname:"URI"
        SETL listContainerType:(val)
        SETL message:(message + uriValue + " " + relType + " " + prefixValue + localIdentifierValue + ".\n" + prefixValue + localIdentifierValue + " a " + listContainerType + ".\n" + prefixValue + localIdentifierValue + " :hasLabel " + nameValue + ".\n")
    }
    IF(intrfCount = 0 AND relType != labelVal)
    {
        SETL message:(message + uriValue + " " + relType + " " + prefixValue + localIdentifierValue + ".\n" + prefixValue + localIdentifierValue + " :hasLabel " + nameValue + ".\n")
    }
    SETL objectsInListX:(array(100))
    SETL objectsInListY:(array(100))
    SETL objectsInListID:(array(100))
    CC "Core" GET_ATTR_VAL objid:(objectId) attrname:"Position"
    SETL position:(val) #lower left corner. to get coordinates of oposed diagonal point, x = x1 + width, y = y1 + height #NODE x:2cm y:9cm w:6cm h:3cm index:15
    SETL rawX:(token(position, 1, " ")) #x:2cm
    SETL rawX:(replall(rawX, "x:", ""))
    SETL xPoz:(replall(rawX, "cm", ""))
    SETL xPoz:(VAL xPoz)
    SETL rawY:(token(position, 2, " "))
    SETL rawY:(replall(rawY, "y:", ""))
    SETL yPoz:(replall(rawY, "cm", ""))
    SETL yPoz:(VAL yPoz)
    SETL rawW:(token(position, 3, " "))
    SETL rawW:(replall(rawW, "w:", ""))
    SETL wVal:(replall(rawW, "cm", ""))
    SETL wVal:(VAL wVal)
    SETL rawH:(token(position, 4, " "))
    SETL rawH:(replall(rawH, "h:", ""))
    SETL hVal:(replall(rawH, "cm", ""))
    SETL hVal:(VAL hVal)
    #CC "AdoScript" VIEWBOX text:(hVal)

    SETL xPoz2:(xPoz + wVal)
    SETL yPoz2:(yPoz + hVal)
    #CC "AdoScript" VIEWBOX text:(STR xPoz2)

    CC "Modeling" GET_OBJECTS_WITHIN_AREA x1:(VAL (STR xPoz + "cm")) y1:(VAL (STR yPoz + "cm")) x2:(VAL (STR xPoz2 + "cm")) y2:(VAL (STR yPoz2 + "cm"))
    SETL elemsInList:(objids)
    SETL index:(0)
    FOR i in:(elemsInList)
    {
        CC "Core" GET_CLASS_ID objid:(VAL i)
        CC "Core" GET_CLASS_NAME classid:(classid)
        IF(classname != "ListContainer")
        {
            #CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"URI"
            #SETL uriVal:(val)
            #CC "AdoScript" VIEWBOX text:(uriVal)
            CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Position"
            SETL position:(val)
            SETL rawX:(token(position, 1, " ")) #x:2cm
            SETL rawX:(replall(rawX, "x:", ""))
            SETL xPoz:(replall(rawX, "cm", ""))
            SETL xPoz:(VAL xPoz)
            SETL rawY:(token(position, 2, " "))
            SETL rawY:(replall(rawY, "y:", ""))
            SETL yPoz:(replall(rawY, "cm", ""))
            SETL objectsInListX[index]:(xPoz)
            SETL objectsInListY[index]:(yPoz)
            SETL objectsInListID[index]:(VAL i)
            SETL index:(index + 1)
        }
    }
    IF(containerType = "Ordered")
    {
        FOR k from:(0) to:(index - 2)
        {
            SETL swapped:(0)
            FOR l from:(0) to:(index - k - 2)
            {
                IF ((objectsInListY[l]) > (objectsInListY[l+1]))
                {
                    SETL tempY:(objectsInListY[l])
                    SETL objectsInListY[l]:(objectsInListY[l+1])
                    SETL objectsInListY[l+1]:(tempY)
                    SETL tempX:(objectsInListX[l])
                    SETL objectsInListX[l]:(objectsInListX[l+1])
                    SETL objectsInListX[l+1]:(tempX)
                    SETL tempID:(objectsInListID[l])
                    SETL objectsInListID[l]:(objectsInListID[l+1])
                    SETL objectsInListID[l+1]:(tempID)
                    SETL swapped:(1)
                }
                IF ((objectsInListY[l]) = (objectsInListY[l+1]))
                {
                    IF((objectsInListX[l]) > (objectsInListX[l+1]))
                    {
                        SETL tempY:(objectsInListY[l])
                        SETL objectsInListY[l]:(objectsInListY[l+1])
                        SETL objectsInListY[l+1]:(tempY)
                        SETL tempX:(objectsInListX[l])
                        SETL objectsInListX[l]:(objectsInListX[l+1])
                        SETL objectsInListX[l+1]:(tempX)
                        SETL tempID:(objectsInListID[l])
                        SETL objectsInListID[l]:(objectsInListID[l+1])
                        SETL objectsInListID[l+1]:(tempID)
                        SETL swapped:(1)
                    }
                }
            }
            IF(swapped = 0)
            {
                BREAK
            }
        }
        SETL containerMessage:("")
        SETL containedIndexMessage:("")
        FOR i from:(0) to:(index - 1)
        {
            SETL containedObjectId:(objectsInListID[i])
            CC "Core" GET_ATTR_VAL objid:(containedObjectId) attrname:"URI"
            SETL containedUriVal:(val)
            IF(i < index - 1)
            {
                SETL containerMessage:(containerMessage + containedUriVal + ", ")
                SETL containedIndexMessage:(containedIndexMessage + containedUriVal + " :index " + STR (i+1) + ".\n")
            }
            IF(i = index - 1)
            {
                SETL containerMessage:(containerMessage + containedUriVal + ".")
                SETL containedIndexMessage:(containedIndexMessage + containedUriVal + " :index " + STR (i+1) + ".\n")
            }
            #CC "AdoScript" VIEWBOX text:(uriVal)
        }
        SETL result:(message + prefixValue + localIdentifierValue + " rdfs:member " + containerMessage + "\n" + containedIndexMessage)
    }
    IF(containerType = "Unordered")
    {
        SETL containerMessage:("")
        FOR i from:(0) to:(index - 1)
        {
            SETL containedObjectId:(objectsInListID[i])
            CC "Core" GET_ATTR_VAL objid:(containedObjectId) attrname:"URI"
            SETL containedUriVal:(val)
            IF(i < index - 1)
            {
                SETL containerMessage:(containerMessage + containedUriVal + ", ")
            }
            IF(i = index - 1)
            {
                SETL containerMessage:(containerMessage + containedUriVal + ".")
            }
            #CC "AdoScript" VIEWBOX text:(uriVal)
        }
        SETL result:(message + prefixValue + localIdentifierValue + " rdfs:member " + containerMessage + "\n")
    }
    #to add support for parsing elements from the list / anonymous node
}

PROCEDURE global GET_THINGIDENTIFIER_ATTRIBUTES objId: integer uriValue: string result: reference #trebuie dname din pas anterior
{
    SETL attributesValues:("")
    CC "Core" GET_CLASS_ID objid:(objId)
    CC "Core" GET_CLASS_NAME classid:(classid)
    CC "Core" GET_ATTR_ID classid:(classid) attrname:"Multi-Language Label"
    SETL attributesId:(attrid)
    CC "Core" GET_ALL_REC_ATTR_ROW_IDS objid:(objId) attrid:(attributesId)
	FOR i in:(rowids)
    {
        CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Label"
		SETL labelValue:(val)
		CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Language"
		SETL languageValue:(val)
        SETL attributesValues:(attributesValues + uriValue + " :hasLabel " + "\"" + labelValue + "\"" + "@" + languageValue + ".\n") #poate totusi sa schimbam numele lui :hasLabel
    }
    CC "Core" GET_ATTR_ID classid:(classid) attrname:"IsOfType"
    SETL attributesId:(attrid)
    CC "Core" GET_INTERREF objid:(objId) attrid:(attributesId)
    CC "Core" GET_INTERREF_COUNT objid:(objId) attrid:(attributesId)
    SETL intrfCount:(count)
    IF(intrfCount > 0)
    {
        SETL modelIdToEnter:(tmodelid)
        SETL thingTypeObjName:(tobjname)
        SETL thingTypeObjClassId:(tclassid)
        CC "Modeling" IS_OPENED modelid:(modelIdToEnter)
        IF (NOT(isopened))
        {
            CC "Modeling" OPEN modelids:(modelIdToEnter)
        }
        CC "Core" GET_OBJ_ID modelid:(modelIdToEnter) classid:(thingTypeObjClassId) objname:(thingTypeObjName)
        CC "Core" GET_ATTR_VAL objid:(objid) attrname:"URI"
        SETL objTypeValue:(val)
        SETL attributesValues:(attributesValues + uriValue + " a " + objTypeValue + ".\n")
    }
    CC "Core" GET_ATTR_ID classid:(classid) attrname:"Attributes" #Aici de modificat si in record
    SETL attributesId:(attrid)
    CC "Core" GET_ALL_REC_ATTR_ROW_IDS objid:(objId) attrid:(attributesId)
	SETL openAnonymousNode:(0)
    SETL openList:(0)
    FOR i in:(rowids)
	{
        CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Prefix"
		SETL prefixValue:(val)
		CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"attributeLocalIdentifier"
		SETL attributeLocalIdentifierValue:(val)
        CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Value"
		SETL valueValue:(val)
        CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Datatype"
		SETL datatypeValue:(val)
        IF(openAnonymousNode = 0 AND openList = 0)
        {
            IF (datatypeValue != "URI")
            {
                IF(datatypeValue = "xsd:string" OR datatypeValue = "xsd:integer")
                {
                    SETL attributesValues:(attributesValues + uriValue + " " + prefixValue + attributeLocalIdentifierValue + " " + "\"" + valueValue + "\"" + "^^" + datatypeValue + ".\n")
                }
                IF(datatypeValue = "[")
                {
                    SETL attributesValues:(attributesValues + uriValue + " " + prefixValue + attributeLocalIdentifierValue + " " + "[")
                    SETL openAnonymousNode:(1)
                }
            }
            ELSE
            {
                SETL attributesValues:(attributesValues + uriValue + " " + prefixValue + attributeLocalIdentifierValue + " " + valueValue + ".\n")
            }
        }
        IF(openAnonymousNode = 1 AND openList = 0)
        {
            IF (datatypeValue != "URI")
            {
                IF(datatypeValue = "xsd:string" OR datatypeValue = "xsd:integer")
                {
                    SETL attributesValues:(attributesValues + prefixValue + attributeLocalIdentifierValue + " " + "\"" + valueValue + "\"" + "^^" + datatypeValue + "; ")
                }
                IF(datatypeValue = "]")
                {
                    SETL attributesValues:(attributesValues + "]" + ".\n")
                    SETL openAnonymousNode:(0)
                }
            }
            ELSE
            {
                SETL attributesValues:(attributesValues + prefixValue + attributeLocalIdentifierValue + " " + valueValue + "; ")
            }
        }
        IF(openList = 0 AND openAnonymousNode = 0)
        {
            IF (datatypeValue != "URI")
            {
                IF(datatypeValue = "xsd:string" OR datatypeValue = "xsd:integer")
                {
                    SETL attributesValues:(attributesValues + uriValue + " " + prefixValue + attributeLocalIdentifierValue + " " + "\"" + valueValue + "\"" + "^^" + datatypeValue + ".\n")
                }
                IF(datatypeValue = "(")
                {
                    SETL attributesValues:(attributesValues + uriValue + " " + prefixValue + attributeLocalIdentifierValue + " " + "(")
                    SETL openList:(1)
                }
            }
            ELSE
            {
                SETL attributesValues:(attributesValues + uriValue + " " + prefixValue + attributeLocalIdentifierValue + " " + valueValue + ".\n")
            }
        }
        IF(openList = 1 AND openAnonymousNode = 0)
        {
            IF (datatypeValue != "URI")
            {
                IF(datatypeValue = "xsd:string" OR datatypeValue = "xsd:integer")
                {
                    SETL attributesValues:(attributesValues + "\"" + valueValue + "\"" + "^^" + datatypeValue + " ")
                }
                IF(datatypeValue = ")")
                {
                    SETL attributesValues:(attributesValues + ")" + ".\n")
                    SETL openList:(0)
                }
            }
            ELSE
            {
                SETL attributesValues:(attributesValues + valueValue + " ")
            }
        }
	}
    CC "Core" GET_ATTR_ID classid:(classid) attrname:"SameAs"
    SETL attributesId:(attrid)
    CC "Core" GET_INTERREF objid:(objId) attrid:(attributesId)
    CC "Core" GET_INTERREF_COUNT objid:(objId) attrid:(attributesId)
    SETL intrfCount:(count)
    IF(intrfCount > 0)
    {
        SETL modelIdToEnter:(tmodelid)
        SETL sameAsObjName:(tobjname)
        SETL sameAsObjClassId:(tclassid)
        CC "Modeling" IS_OPENED modelid:(modelIdToEnter)
        IF (NOT(isopened))
        {
            CC "Modeling" OPEN modelids:(modelIdToEnter)
        }
        CC "Core" GET_OBJ_ID modelid:(modelIdToEnter) classid:(sameAsObjClassId) objname:(sameAsObjName)
        CC "Core" GET_ATTR_VAL objid:(objid) attrname:"URI"
        SETL sameAsObjectValue:(val)
        SETL attributesValues:(attributesValues + uriValue + " owl:sameAs " + sameAsObjectValue + ".\n")
    }
    SETL result:(attributesValues)
}

#-------------------------------------------------------------------------------------------------------------------------------------------------#

CC "Modeling" GET_ACT_MODEL
SETL actModelId: (modelid)

CC "Core" GET_ATTR_VAL objid:(actModelId) attrname:"TriG Graph Prefix"
SETL modelPrefix:(val)

CC "Core" GET_ATTR_VAL objid:(actModelId) attrname:"TriG Graph Local identifier"
SETL modelLocalIdentifier:(val)

SETL prefixes:("")
SETL endMessage:("")
GET_MODEL_PREFIXES modelId:(actModelId) result:prefixes

IF(modelPrefix != "" AND modelLocalIdentifier != "")
{
    SETL message:(modelPrefix + modelLocalIdentifier + "\n" + "{" + "\n")
    SETL message:(prefixes + message)
    SETL endMessage:("\n" + "}")

}
ELSE
{
    SETL message:("")
    SETL message:(message + prefixes)
}

#SETL message:("")

CC "Core" GET_ALL_OBJS_OF_CLASSNAME modelid:(actModelId) classname:"ThingIdentifier"
SETL thingIdentifiersObjids:(objids)
FOR i in:(thingIdentifiersObjids)
{
    CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"URI"
    SETL uriValue:(val)
    CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Label"
    SETL label:(val)
    SETL message:(message + uriValue + " :hasLabel " + "\"" + label + "\"" + ".\n")
    GET_THINGIDENTIFIER_ATTRIBUTES objId:(VAL i) uriValue:(uriValue) result:objAttrs
    SETL message:(message + objAttrs)
    SETL objAttrs:("")
    
    #connectors
    CC "Core" GET_CONNECTORS objid:(VAL i) out
    SETL connectorsIds:(objids)
    FOR j in:(connectorsIds)
    {
        CC "Core" GET_CONNECTOR_ENDPOINTS objid:(VAL j)
        IF(j != i)
        {
            CC "Core" GET_CLASS_ID objid:(toobjid)
            CC "Core" GET_CLASS_NAME classid:(classid)
            IF(classname = "ThingType")
            {
                GET_THINGTYPE_INFO objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
            IF(classname = "ThingIdentifier")
            {
                GET_THINGIDENTIFIER_INFO connectorId:(VAL j) objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
            IF(classname = "Data")
            {
                GET_DATA_INFO connectorId:(VAL j) objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
            IF(classname = "HelperNode")
            {
                GET_HELPER_NODE_INFO connectorId:(VAL j) objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
            IF(classname = "ListContainer")
            {
                GET_LIST_CONTAINER_INFO fromObject:(VAL i) connectorId:(VAL j) objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
        }
    }
}

#Helper Nodes relationships parsing
CC "Core" GET_ALL_OBJS_OF_CLASSNAME modelid:(actModelId) classname:"HelperNode"
SETL helperNodesObjids:(objids)
FOR i in:(helperNodesObjids)
{
    CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"Local identifier"
    SETL localIdentifierValue:(val)
    SETL helperNodeUriValue:("_:" + localIdentifierValue)
    
    #connectors
    CC "Core" GET_CONNECTORS objid:(VAL i) out
    SETL connectorsIds:(objids)
    FOR j in:(connectorsIds)
    {
        CC "Core" GET_CONNECTOR_ENDPOINTS objid:(VAL j)
        IF(j != i)
        {
            CC "Core" GET_CLASS_ID objid:(toobjid)
            CC "Core" GET_CLASS_NAME classid:(classid)
            IF(classname = "ThingType")
            {
                GET_THINGTYPE_INFO objectId:(toobjid) uriValue:(helperNodeUriValue) message:(message) result: message
            }
            IF(classname = "ThingIdentifier")
            {
                GET_THINGIDENTIFIER_INFO connectorId:(VAL j) objectId:(toobjid) uriValue:(helperNodeUriValue) message:(message) result: message
            }
            IF(classname = "Data")
            {
                GET_DATA_INFO connectorId:(VAL j) objectId:(toobjid) uriValue:(helperNodeUriValue) message:(message) result: message
            }
            IF(classname = "HelperNode")
            {
                GET_HELPER_NODE_INFO connectorId:(VAL j) objectId:(toobjid) uriValue:(helperNodeUriValue) message:(message) result: message
            }
            IF(classname = "ListContainer")
            {
                GET_LIST_CONTAINER_INFO fromObject:(VAL i) connectorId:(VAL j) objectId:(toobjid) uriValue:(helperNodeUriValue) message:(message) result: message
            }
        }
    }
}

CC "Core" GET_ALL_OBJS_OF_CLASSNAME modelid:(actModelId) classname:"ThingType"
SETL thingTypesObjids:(objids)
FOR i in:(thingTypesObjids)
{
    CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"URI"
    SETL uriValue:(val)
    SETL message:(message + uriValue + " rdfs:subClassOf " + "schema:Thing." + "\n")
    
    #connectors
    CC "Core" GET_CONNECTORS objid:(VAL i) out
    SETL connectorsIds:(objids)
    FOR j in:(connectorsIds)
    {
        CC "Core" GET_CONNECTOR_ENDPOINTS objid:(VAL j)
        IF(j != i)
        {
            CC "Core" GET_CLASS_ID objid:(toobjid)
            CC "Core" GET_CLASS_NAME classid:(classid)
            SETL checkClassname:(classname)
            IF(checkClassname = "ThingType")
            {
                GET_THINGTYPE_INFO_FULL connectorId:(VAL j) objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
            IF(checkClassname = "Property")
            {
                GET_PROPERTY_INFO connectorId:(VAL j) objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
            IF(checkClassname = "DatatypeNode")
            {
                GET_DATATYPE_NODE_INFO connectorId:(VAL j) objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
        }
    }
}

#properties
CC "Core" GET_ALL_OBJS_OF_CLASSNAME modelid:(actModelId) classname:"Property"
SETL propertyObjids:(objids)
FOR i in:(propertyObjids)
{
    CC "Core" GET_ATTR_VAL objid:(VAL i) attrname:"URI"
    SETL uriValue:(val)
    SETL message:(message + uriValue + " rdfs:subPropertyOf " + "schema:Property." + "\n")
    
    #connectors
    CC "Core" GET_CONNECTORS objid:(VAL i) out
    SETL propertyConnectorsIds:(objids)
    FOR j in:(propertyConnectorsIds)
    {
        CC "Core" GET_CONNECTOR_ENDPOINTS objid:(VAL j)
        IF(j != i)
        {
            CC "Core" GET_CLASS_ID objid:(toobjid)
            CC "Core" GET_CLASS_NAME classid:(classid)
            SETL checkClassname:(classname)
            IF(checkClassname = "ThingType")
            {
				GET_THINGTYPE_INFO_FULL connectorId:(VAL j) objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
            IF(checkClassname = "Property")
            {
                GET_PROPERTY_INFO connectorId:(VAL j) objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
            IF(checkClassname = "DatatypeNode")
            {
                GET_DATATYPE_NODE_INFO connectorId:(VAL j) objectId:(toobjid) uriValue:(uriValue) message:(message) result: message
            }
        }
    }
}
#CC "AdoScript" VIEWBOX text:(message)

#Write file 
#SETL filePath:(".Graph_Modelling_Tool\\Scripts\\content_graph.ttl")

IF(endMessage != "")
{
    SETL message:(message + endMessage)
    SETL filePath:("D:\\Andrei\\ModellingTools\\Graph_Modelling_Tool\\Scripts\\raw_content_graph.trig")
    CC "AdoScript" FWRITE file:(filePath) text:(message) binary:0
    CC "AdoScript" SLEEP ms:500
    #Deletion of duplicate statements from the graph
    SETL deletionScriptFilePath:("D:\\Andrei\\ModellingTools\\Graph_Modelling_Tool\\Scripts\\trig_delete_duplicate_statements.py")
    START ("python " + deletionScriptFilePath)
    #CC "AdoScript" SLEEP ms:500
    SETL t2: (getTickCount())
    SETL timesec: ((t2-t1)*0.001)
    CC "AdoScript" INFOBOX ("The execution of the script took " + STR timesec+ " seconds.")
    SETL readFilePath:("D:\\Andrei\\ModellingTools\\Graph_Modelling_Tool\\Scripts\\content_graph.trig")
    CC "AdoScript" FREAD file:(readFilePath)
    CC "AdoScript" VIEWBOX text:(text)
}
ELSE
{
    SETL filePath:("D:\\Andrei\\ModellingTools\\Graph_Modelling_Tool\\Scripts\\raw_content_graph.ttl")
    CC "AdoScript" FWRITE file:(filePath) text:(message) binary:0
    CC "AdoScript" SLEEP ms:500
    #Deletion of duplicate statements from the graph
    SETL deletionScriptFilePath:("D:\\Andrei\\ModellingTools\\Graph_Modelling_Tool\\Scripts\\delete_duplicate_statements.py")
    START ("python " + deletionScriptFilePath)
    #CC "AdoScript" SLEEP ms:500
    SETL t2: (getTickCount())
    SETL timesec: ((t2-t1)*0.001)
    CC "AdoScript" INFOBOX ("The execution of the script took " + STR timesec+ " seconds.")
    SETL readFilePath:("D:\\Andrei\\ModellingTools\\Graph_Modelling_Tool\\Scripts\\content_graph.ttl")
    CC "AdoScript" FREAD file:(readFilePath)
    CC "AdoScript" VIEWBOX text:(text)
}