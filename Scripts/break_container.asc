CC "Modeling" GET_ACT_MODEL
SETL actModelId: (modelid)

CC "Core" GET_CLASS_ID classname:"RelatedTo"
SETL relatedToClassId: (classid)

CC "Core" GET_CLASS_ID classname:"ListContainer"
SETL listContainerClassID: (classid)

CC "Modeling" GET_SELECTED modelid:(actModelId)

SETL nCurrentObjID:(VAL token(objids, 0, " "))

CC "Core" GET_CLASS_ID objid:(nCurrentObjID)
CC "Core" GET_CLASS_NAME classid:(classid)

IF (classname = "ListContainer")
{
    CC "Core" GET_CONNECTORS objid:(nCurrentObjID) in
    FOR i in:(objids)
    {
        CC "Core" GET_CLASS_ID objid:(VAL i)
        CC "Core" GET_CLASS_NAME classid:(classid)
        IF(classname = "RelatedToList")
        {
            SETL connectorID:(VAL i)
        }
    }

    CC "Core" GET_ATTR_VAL objid:(connectorID) attrname:("Prefix")
    SETL connectorPrefix:(val)
    CC "Core" GET_ATTR_VAL objid:(connectorID) attrname:("Local identifier")
    SETL connectorLocalIdentifier:(val)

    CC "Core" GET_CONNECTOR_ENDPOINTS objid:(connectorID)
    SETL fromObj:(fromobjid)

    #CC "AdoScript" VIEWBOX text:(STR fromObj)
    
    CC "Core" GET_ATTR_VAL objid:(nCurrentObjID) attrname:("Name")
    SETL listContainerName:(val)

    SETL aqlExpr:("{" + "\"" +listContainerName + "\"" + ":\"ListContainer" + "\"" + "}<-" + "\"" + "Is inside" + "\"")

    CC "AQL" EVAL_AQL_EXPRESSION expr:(aqlExpr) modelid:(actModelId)
    SETL insideObjIds:(objids)
    FOR i in:(insideObjIds) { #currently just for "ThingIdentifiers"
        CC "Core" GET_CLASS_ID objid:(VAL i)
        CC "Core" GET_CLASS_NAME classid:(classid)
        IF (classname = "ThingIdentifier")
        {
            CC "Core" CREATE_CONNECTOR modelid:(actModelId) fromobjid:(fromObj) toobjid:(VAL i) classid:(relatedToClassId)
            CC "Core" SET_ATTR_VAL objid:(objid) attrname:("Prefix") val:(connectorPrefix)
            CC "Core" SET_ATTR_VAL objid:(objid) attrname:("Local identifier") val:(connectorLocalIdentifier)
        }
    }

    CC "Core" DELETE_OBJ modelid:(actModelId) objid:(nCurrentObjID)
}
ELSE 
{
    CC "AdoScript" VIEWBOX text:("This operation is permitted only for ListContainer elements!")
}